// This is the OpenQasm 2.0 grammar
start: "OPENQASM" real ";" statement+

?statement: decl
    | _gatedecl "{" gbody "}" -> gatedef
    | "opaque" id ["(" [idlist] ")"] idlist ";" -> opaque
    | qop
    | "if" "(" id "==" nninteger ")" qop -> ifstatement
    | "include" FILENAME ";" -> include
    | "barrier" params ";" -> barrier

gbody: gop*

decl: "qreg" id "[" nninteger "]" ";" -> qregdecl
    | "creg" id "[" nninteger "]" ";" -> cregdecl

// A gate declaration has an optional list of parameters (ids)
_gatedecl: "gate" id ["(" [idlist] ")"] idlist

// Note that only idlist is allowed within a gate declaration
?gop: standardgate
    | "barrier" idlist ";" -> gbarrier
    | id ["(" [explist] ")"] idlist ";" -> gcall


?standardgate: "U" "(" explist ")" argument ";" -> ugate
    | "CX" argument "," argument ";" -> cxgate

qop: standardgate
    | "measure" argument "->" argument ";" -> measure
    | "reset" argument ";" -> reset
    | id ["(" [explist] ")"] params ";" -> qcall

idlist: id ("," id)*

// superset of idlist, also allowing optional array indices
params: argument ("," (argument))*
args: argument ("," (argument))*

argument: id ["[" nninteger "]"]

explist: exp ("," exp)*

// We split up expression by their order of associativity (lowest-to-highest)
?exp: product
    | exp "+" product -> add
    | exp "-" product -> sub

?product: exponent
    | product "*" exponent -> mul
    | product "/" exponent -> div

?exponent: atom
    | exponent "^" atom -> exponent

?atom: real
    | nninteger
    | "pi" -> pi
    | id -> reference
    | "-" atom -> neg
    | "(" exp ")"
    | mathfunop "(" exp ")" -> mathfun

!mathfunop: "sin"
    | "cos"
    | "tan"
    | "exp"
    | "ln"
    | "sqrt"



%import common.DIGIT
%import common.FLOAT -> REAL
real: REAL

NNDIGIT : "1".."9"
NNINTEGER : "0"
    | NNDIGIT (DIGIT)*
nninteger: NNINTEGER

// Since only filenames are quoted by '"', we can capture all groups between them as a filename.
%import common.ESCAPED_STRING -> FILENAME
ID: /[a-z][A-Za-z0-9]*/
id: ID

COMMENT: /\/\/.*/
%ignore COMMENT

%import common.WS
%ignore WS
